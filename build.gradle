// ./gradlew clean && ./gradlew uploadArchives -Prelease

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: "signing"

group 'me.geso.string_filter'

version '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

compileJava {
    options.compilerArgs << '-XDignore.symbol.file' << '-Xlint:all'
}

artifacts {
    archives javadocJar, sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

//set build variables based on build type (release, continuous integration, development)
def isDevBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
    version += "-SNAPSHOT"
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
        else {
            mavenDeployer {
                if(isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'me.geso.string_filter'
                    packaging 'jar'
                    description 'String filter'
                    url 'https://github.com/tokuhirom/string_filter'

                    scm {
                        url "scm:git@github.com:tokuhirom/string_filter.git"
                        connection "scm:git@github.com:tokuhirom/string_filter.git"
                        developerConnection "scm:git@github.com:tokuhirom/string_filter.git"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'tokuhirom'
                            name 'Tokuhiro Matsuno'
                            email 'tokuhirom@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    compile "me.geso:regexp-trie:1.0.5"
    testCompile "org.apache.commons:commons-lang3:3.4"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
